buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

//https://www.thoughtworks.com/insights/blog/signing-open-source-android-apps-without-disclosing-passwords
def appSecrets = new Properties()
appSecrets.load(new FileInputStream(rootProject.file("app-secret.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.greenlionsoft.pollution.madrid"
        minSdkVersion 14
        //targeting 23 implies new permission system
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            storeFile file(appSecrets.storeFile)
            storePassword appSecrets.storePassword
            keyAlias appSecrets.keyAlias
            keyPassword appSecrets.keyPassword
        }
    }



    buildTypes {

        debug {
            resValue "string", "google_maps_v2_api_key", appSecrets.googleMapV2DebugKey
            resValue "string", "analytics_tracking_id", appSecrets.analyticsTrackingId
            resValue "string", "admob_id", appSecrets.admobId
            resValue "string", "pollution_end_point", appSecrets.pollutionEndPoint
            minifyEnabled false
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-DEBUG"
        }

        release {
            resValue "string", "google_maps_v2_api_key", appSecrets.googleMapV2ReleaseKey
            resValue "string", "analytics_tracking_id", appSecrets.analyticsTrackingId
            resValue "string", "admob_id", appSecrets.admobId
            resValue "string", "pollution_end_point", appSecrets.pollutionEndPoint
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def date = new Date();
            def formattedDate = date.format('yyyyMMdd_HHmm')
            output.outputFile = new File(output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "_MadridPollution_VC" + defaultConfig.versionCode + "_" + defaultConfig.versionName + "_" + ".apk")
            )
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //ButterKnife Required
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    //Android
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    //Services
    compile 'com.google.android.gms:play-services-maps:8.3.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.google.android.gms:play-services-ads:8.3.0'
    //ButterKnife
    compile 'com.jakewharton:butterknife:7.0.1'
    //Retrofit
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    //JodaTime
    compile 'joda-time:joda-time:2.9.1'
    compile 'joda-time:joda-time:2.9.1'
    //Ripple Effect
    compile 'com.balysv:material-ripple:1.0.2'
    //Gson JSON handling
    compile 'com.google.code.gson:gson:2.4'
    //Modules dependencies
    compile project(':presentation')
    compile project(':domain')
    compile project(':repository')
    //Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true;
    }
}